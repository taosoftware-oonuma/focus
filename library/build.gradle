buildscript {
    dependencies {
        classpath rootProject.ext.androidPlugin
        classpath rootProject.ext.semverPlugin
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.ethankhall.semantic-versioning'

project.version.with {
    major = 0;
    minor = 3;
    patch = 1;
    releaseBuild = true
}

project.ext.set("VERSION_NAME",
        project.version.toString())

project.ext.set("VERSION_CODE",
        project.version.major * 10000 + project.version.minor * 1000 + project.version.patch * 100)

dependencies {
    compile rootProject.ext.supportV4
    compile rootProject.ext.supportAnnotations

    androidTestCompile rootProject.ext.testingSupportLib
    androidTestCompile(rootProject.ext.truth) {
        exclude group: 'junit' // Android ships with JUnit.
    }

    testCompile rootProject.ext.junit
    testCompile rootProject.ext.truth
    testCompile rootProject.ext.mockito
}

android {
    compileSdkVersion 22
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 22
        versionCode VERSION_CODE
        versionName VERSION_NAME
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    sourceSets {
        main {
            java {
                srcDirs = [
                        'src/main/java',
                        '../third-party/android-ex/camera2/portability/src',
                        '../third-party/android-ex/camera2/public/src',
                        '../third-party/android-ex/camera2/utils/src'
                ]
            }
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }
}

apply from: 'https://raw.github.com/chrisbanes/gradle-mvn-push/master/gradle-mvn-push.gradle'
