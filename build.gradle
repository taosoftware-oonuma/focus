allprojects {
    buildscript {
        repositories {
            mavenCentral()
            jcenter()
        }
    }

    repositories {
        mavenCentral()
    }
}

def String getWorkingBranch() {
    'git rev-parse --abbrev-ref HEAD'.execute([], rootDir).text.trim()
}

def boolean isDirty() {
    'git describe --dirty'.execute([], rootDir).text.trim().contains("dirty")
}

def boolean isReleaseBuild() {
    getWorkingBranch().equals('master') && !isDirty()
}

def int getVersionCode() {
    Integer.parseInt('git rev-list --first-parent --count HEAD'.execute([], rootDir).text.trim())
}

def String getVersionName() {
    def versionPropsFile = file('version.properties')
    def Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(versionPropsFile))
    def version = versionProps['major'] + "." + versionProps['minor'] + "." + versionProps['patch']
    return isReleaseBuild() ? version : version.concat("-SNAPSHOT");
}

task bumpMajor << {
    ant.propertyfile(file: 'version.properties') {
        entry(key: 'major', type: 'int', operation: '+', value: 1)
        entry(key: 'minor', type: 'int', operation: '=', value: 0)
        entry(key: 'patch', type: 'int', operation: '=', value: 0)
    }
}

task bumpMinor << {
    ant.propertyfile(file: 'version.properties') {
        entry(key: 'minor', type: 'int', operation: '+', value: 1)
        entry(key: 'patch', type: 'int', operation: '=', value: 0)
    }
}

task bumpPatch << {
    ant.propertyfile(file: 'version.properties') {
        entry(key: 'patch', type: 'int', operation: '+', value: 1)
    }
}

task version << {
    println getVersionName()
}

ext {
    minSdkVersion = 17
    compileSdkVersion = 22
    targetSdkVersion = compileSdkVersion
    buildToolsVersion = '22.0.1'
    javaVersion = JavaVersion.VERSION_1_7

    androidPlugin = 'com.android.tools.build:gradle:1.3.0-beta4'

    supportV4 = 'com.android.support:support-v4:22.1.1'
    supportAnnotations = 'com.android.support:support-annotations:22.1.1'

    VERSION_NAME = getVersionName()
    VERSION_CODE = getVersionCode()

    GROUP = 'com.obviousengine.android.focus'

    POM_URL = 'https://github.com/obviousengineering/focus'
    POM_SCM_URL = 'https://github.com/obviousengineering/focus'
    POM_SCM_CONNECTION = 'scm:git@github.com:obviousengineering/focus.git'
    POM_SCM_DEV_CONNECTION = 'scm:git@github.com:obviousengineering/focus.git'
    POM_LICENCE_NAME = 'The Apache Software License, Version 2.0'
    POM_LICENCE_URL = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    POM_LICENCE_DIST = 'repo'
    POM_DEVELOPER_ID = 'seene'
    POM_DEVELOPER_NAME = ' Seene, Inc.'
}